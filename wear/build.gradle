apply plugin: 'com.android.application'
apply plugin: 'android-apt'


def PACKAGE_NAME = "com.devoxx"
def VERSION_CODE = 111
def VERSION_NAME = "1.0.${VERSION_CODE}"

def keystorePropertiesFile = rootProject.file("devoxx.properties");
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def mapKey = keystoreProperties['mapKey']

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId PACKAGE_NAME
        minSdkVersion 21
        targetSdkVersion 22
        versionCode VERSION_CODE
        versionName VERSION_NAME
    }

    signingConfigs {
        debug {
            def signFilePath = "${project(':app').projectDir}/debug.keystore"
            storeFile file(signFilePath)
        }
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            def signFilePath = "${project(':app').projectDir}/${keystoreProperties['storeFile']}"
            storeFile file(signFilePath)
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            buildConfigField "boolean", "LOGGING", "false"
            buildConfigField "boolean", "DEV_ROTATION_ALL", "false"
            manifestPlaceholders = [applicationLabel: "@string/app_name", mapKey: mapKey]
            signingConfig signingConfigs.release
        }
        devoxx {
            debuggable true
            versionNameSuffix "@devoxx"
            buildConfigField "boolean", "LOGGING", "true"
            buildConfigField "boolean", "DEV_ROTATION_ALL", "false"
            signingConfig signingConfigs.debug
            manifestPlaceholders = [applicationLabel: "@string/app_name", mapKey: mapKey]
        }
        devoxxDev {
            debuggable true
            versionNameSuffix "@devoxx"
            buildConfigField "boolean", "LOGGING", "true"
            buildConfigField "boolean", "DEV_ROTATION_ALL", "true"
            signingConfig signingConfigs.debug
            manifestPlaceholders = [applicationLabel: "@string/app_name", mapKey: mapKey]
        }
    }
}

apt {
    arguments {
        androidManifestFile variant.outputs[0].processResources.manifestFile
        resourcePackageName PACKAGE_NAME
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.google.android.support:wearable:1.3.0'
    compile 'com.google.android.gms:play-services-wearable:8.4.0'


    apt 'org.androidannotations:androidannotations:3.3.2'
    compile 'org.androidannotations:androidannotations-api:3.3.2'

    // Event Bus: BusWear
    compile 'pl.tajchert:buswear:0.9.5'

    // TODO: update to 2.0
    // Circled image
    compile 'de.hdodenhof:circleimageview:1.3.0'

    // Org Apache commons lang
    compile 'org.apache.commons:commons-lang3:3.4'

    compile project(path: ':common')


}
